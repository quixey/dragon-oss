allprojects {
    apply plugin: 'idea'
    version = new File("${rootProject.projectDir}/VERSION").text.replaceAll("\\s+", "")
}

configure(allprojects.findAll { it.buildFile != null }) { project ->

    apply plugin: 'java'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    // UTF-8 should be standard by now. So use it!
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    repositories {
        // required for report-ng 1.1.3
        mavenCentral()
        maven { url 'http://clojars.org/repo' }
    }

    configurations {
        provided
    }

    idea {
        module {
            scopes.PROVIDED.plus += configurations.provided
        }
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
        }
        test {
            compileClasspath += configurations.provided
        }
    }

    // add classpaths from 'provided' to javadoc task
    tasks.withType(Javadoc) {
        classpath += configurations.provided
    }

    // rename project archive name
    tasks.withType(Jar) {
        baseName = "${project.name}"
    }

    // add xlint flags
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }

    jar {
        manifest {
            attributes('Implementation-Title': baseName,
                       'Implementation-Version': version,
                       'Build-Time-ISO-8601': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"))
        }
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        from "${project.buildDir}/docs/javadoc"
        classifier = 'javadoc'
    }

    task sourcesJar(type: Jar) {
        from project.sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}
